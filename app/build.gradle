apply plugin: 'com.android.application'

android {
    def endpointProperties = new Properties()
    file(endpointPropertiesPath).withInputStream { endpointProperties.load(it) }
    def endpointFormatter = endpointProperties.getProperty("endpoint_formatter")
    def endpointUsername = endpointProperties.getProperty("username")

    def keyProperties = new Properties()
    file(keyPropertiesPath).withInputStream { keyProperties.load(it) }
    def googleMapsKey = keyProperties.getProperty("google_maps")

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.tehnatha.earthquake"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [mapApiKey: googleMapsKey]
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    flavorDimensions "region"
    productFlavors {
        pacific {
            dimension "region"
            applicationIdSuffix ".main"
            versionNameSuffix "-main"

            def regionProperties = new Properties()
            file("$regionPropertiesPath/main.properties").withInputStream {
                regionProperties.load(it)
            }

            def north = regionProperties.getProperty("north")
            def south = regionProperties.getProperty("south")
            def east = regionProperties.getProperty("east")
            def west = regionProperties.getProperty("west")

            buildConfigField "String", "END_POINT", "\"${String.format(endpointFormatter, north, south, east, west, endpointUsername)}\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def room_version = "2.1.0-rc01"
    def lifecycle_version = "2.0.0"

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha05'

    implementation 'androidx.cardview:cardview:1.0.0'

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-maps:16.1.0'

    implementation 'com.google.code.gson:gson:2.8.5'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
}
